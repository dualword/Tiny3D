#include "shader/util.glsl"
#include "shader/triangle.glsl"
#include "shader/hiz.glsl"

layout(local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE) in;

layout(binding = 0) uniform sampler2D texDepth;
uniform BindlessSampler2D distortionTex, roadTex, heightTex, heightNormal;
uniform vec3 mapTrans, mapScale;
uniform vec4 mapInfo;
uniform float time;
uniform vec3 eyePos;
uniform mat4 viewProjectMatrix;
uniform mat4 viewMatrix;
uniform float fullSize;

uniform mat4 prevVPMatrix;
uniform vec2 uSize;
uniform vec2 uCamParam;
uniform float uMaxLevel;
uniform ivec2 refChunk;

const float WindStrength = 0.65;
const vec2 WindFrequency = vec2(0.05, 0.05);

layout(binding = 1, std430) buffer OutPose {
	mat4 outPose[];
};

layout(binding = 2, std430) buffer IndirectBuff {
	uint  count;
	uint  instanceCount;
	uint  first;
	uint  baseInstance;
} indirect;

mat3 AngleAxis3x3(float angle, vec3 axis) {
	float c = cos(angle), s = sin(angle);

	float t = 1.0 - c;
	float x = axis.x;
	float y = axis.y;
	float z = axis.z;
	
	return mat3(
		t * x * x + c, t * x * y + s * z, t * x * z - s * y,
		t * x * y - s * z, t * y * y + c, t * y * z + s * x,
		t * x * z + s * y, t * y * z - s * x, t * z * z + c
	);
}

mat3 GetWindMat(vec2 pos) {
	vec2 uv = pos * 0.01 + WindFrequency * time;
	vec2 windSample = texture(distortionTex, uv).xy * 2.0 - 1.0;
	windSample *= WindStrength;
	vec3 wind = normalize(vec3(windSample, 0.0));
	mat3 windRotation = AngleAxis3x3(PI * windSample.x, wind);
	return windRotation;
}

void main() {
	uvec2 cur = gl_GlobalInvocationID.xy;
	vec2 dPose = vec2(fullSize * 0.5) - vec2(cur);
	
	float spread = 0.25 + length(dPose) * 0.004;
	vec3 worldPose = vec3(0.0);
	worldPose.xz = floor(eyePos.xz * 0.02) * 50.0 + dPose * spread;
	// Random grass xz position
	vec2 dRand = vec2(random(worldPose, 0.1), random(worldPose, 0.2));
	worldPose.xz += dRand * spread;

	float fStepSize = float(STEP_SIZE), fChunkSize = float(CHUNK_SIZE);
	vec2 infoSize = mapInfo.zw - vec2(fStepSize);

	vec2 coord = (worldPose.xz - mapTrans.xz) / (mapScale.xz * infoSize);
	if(any(bvec2(any(lessThan(coord, vec2(0.0))), any(greaterThan(coord, vec2(1.0)))))) return;
	else if(texture(roadTex, coord).r > 0.001) return;

	vec2 grassChunk = floor((worldPose.xz - mapTrans.xz) / (mapScale.xz * fStepSize * fChunkSize));
	float lodLevel = float(GetChunkLevel(refChunk, ivec2(grassChunk)));

	// Caculate grass pos y;
	vec2 texSize = (infoSize / mapInfo.x) / lodLevel;
	vec2 texPos = coord * texSize;
	vec2 texLD = floor(texPos), texRT = ceil(texPos);
	if(abs(texLD.x - texRT.x) < 0.00001) {
		if(texLD.x > 0.00001) texLD.x -= 1.0;
		else texRT.x += 1.0;
	}
	if(abs(texLD.y - texRT.y) < 0.00001) {
		if(texLD.y > 0.00001) texLD.y -= 1.0;
		else texRT.y += 1.0;
	}
	
	vec2 invTexSize = vec2(1.0) / texSize;
	vec2 coord1 = texLD * invTexSize;
	vec2 coord2 = texRT * invTexSize;
	vec2 coord3 = vec2(texLD.x, texRT.y) * invTexSize;

	vec3 p1 = vec3(coord1.x, texture(heightTex, coord1).x, coord1.y);
	vec3 p2 = vec3(coord2.x, texture(heightTex, coord2).x, coord2.y);
	vec3 p3 = vec3(coord3.x, texture(heightTex, coord3).x, coord3.y);

	if(CheckIn(coord, p1, p2, p3)) 
		worldPose.y = CaculateY(coord, p1, p2, p3);
	else {
		vec2 coord4 = vec2(texRT.x, texLD.y) * invTexSize;
		vec3 p4 = vec3(coord4.x, texture(heightTex, coord4).x, coord4.y);
		worldPose.y = CaculateY(coord, p1, p4, p2);
	}
	worldPose.y = worldPose.y * 255.0 * mapScale.y + mapTrans.y;

	// Caculate occlusion
	float viewz = (viewMatrix * vec4(worldPose, 1.0)).z;
	if(viewz > 0.0) return;
	vec4 projPose = viewProjectMatrix * vec4(worldPose, 1.0);
	if(any(bvec2(any(greaterThan(projPose.xyz, vec3(projPose.w))), projPose.x < -projPose.w))) return;
	
	float rand = (dRand.x + dRand.y) * 0.5;	
	vec2 size = vec2(rand * 0.5 + 0.5, rand * 2.5 + 2.5);

	vec4 topPose = vec4(worldPose + vec3(0.0, size.y, 0.0), 1.0);
	vec4 projTop = viewProjectMatrix * topPose;
	if(projTop.y < -projTop.w) return;

	vec4 leftPose = vec4(worldPose + vec3(-size.x, 0.0, 0.0), 1.0);
	vec4 rightPose = vec4(worldPose + vec3(size.x, 0.0, 0.0), 1.0);

	if(HizQuery(prevVPMatrix, texDepth, uSize, uCamParam, uMaxLevel, 
		topPose, leftPose, rightPose, 0.0001)) return;

	// Wind effect
	mat3 rotMat = RotY(rand * PI);
	mat3 windMat = GetWindMat(worldPose.xz + vec2(viewz));
	rotMat = rotMat * windMat;
	
	// Caculate transform & normal
	mat4 trans = Translate(worldPose) * M3ToM4(rotMat) * Scale(vec3(size, 1.0));
	trans = transpose(trans);
	trans[3] = vec4((texture(heightNormal, coord).xyz - 0.5) * 2.0, 1.0); // normal

	// Output result & add grass instance count
	outPose[atomicAdd(indirect.instanceCount, 1)] = trans;
}
